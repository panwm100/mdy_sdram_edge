/*********www.mdy-edu.com Ã÷µÂÑï¿Æ½Ì ×¢ÊÍ¿ªÊ¼****************
Ã÷µÂÑï×¨×¢FPGAÅàÑµºÍÑĞ¾¿£¬²¢³Ğ½ÓFPGAÏîÄ¿£¬±¾ÏîÄ¿´úÂë½âÊÍ¿ÉÔÚÃ÷µÂÑï¹Ù·½ÂÛÌ³Ñ§Ï°£¨http://www.fpgabbs.cn/£©£¬Ã÷µÂÑïÕÆÎÕÓĞPCIE£¬MIPI£¬ÊÓÆµÆ´½ÓµÈ¼¼Êõ£¬Ìí¼ÓQÈº97925396»¥ÏàÌÖÂÛÑ§Ï°
**********www.mdy-edu.com Ã÷µÂÑï¿Æ½Ì ×¢ÊÍ½áÊø****************/

module sdram_top(
        clk         ,
        clk_100M    ,
        rst_n       ,

        din_1       ,//å½©è‰²å›¾åƒ
        din_vld_1   ,
        din_sop_1   ,
        din_eop_1   ,

        din_2       ,//äºŒå€¼å›¾åƒ
        din_vld_2   ,
        din_sop_2   ,
        din_eop_2   ,

        dout_1      ,//å½©è‰²å›¾åƒ
        dout_vld_1  ,
        dout_sop_1  ,
        dout_eop_1  ,
        dout_usedw_1,
        b_rdy_1     ,

        dout_2      ,//äºŒå€¼å›¾åƒ
        dout_vld_2  ,
        dout_sop_2  ,
        dout_eop_2  ,
        dout_usedw_2,
        b_rdy_2     ,
        key_vld     ,

        //ç¡¬ä»¶æ¥å£
        sd_clk      ,
        cke         ,
        cs          ,
        ras         ,
        cas         ,
        we          ,
        dqm         ,
        sd_addr     ,
        sd_bank     ,

        dq_in       ,
        dq_out      ,
        dq_out_en   ,

        //æµ‹è¯•æ¥å£ï¼Œç”¨ååˆ é™¤
        flag_sel    ,    
        end_cnt0    ,
        end_cnt1    ,
        add_cnt0    ,
        add_cnt1    ,

        
        wr_color_en ,
        wr_sobel_en ,
        rd_color_en ,
        rd_sobel_en ,

        rd_sobel_end,
        rd_color_end,
        wr_sobel_end,
        wr_color_end
    );
    //ä½¿ç”¨æ–¹æ³•ï¼š
    //din_1ï¼ˆsop,eop,vldï¼‰ è¾“å…¥ 16ä½çš„å½©è‰²æ•°æ®æµ ï¼Œå›¾åƒå›ºå®šä¸º640*480ï¼ï¼ï¼
    //din_2ï¼ˆsop,eop,vldï¼‰ è¾“å…¥ 16ä½äºŒå€¼å›¾åƒæ•°æ®æµ ï¼Œå›¾åƒå›ºå®šä¸º640*480ï¼ï¼ï¼

    //dout_1ï¼ˆsop,eop,vldï¼‰ è¾“å‡º 16ä½çš„å½©è‰²æ•°æ®æµ ï¼Œå›¾åƒå›ºå®šä¸º640*480ï¼ï¼ï¼
    //dout_2ï¼ˆsop,eop,vldï¼‰ è¾“å‡º 16ä½äºŒå€¼å›¾åƒæ•°æ®æµ ï¼Œå›¾åƒå›ºå®šä¸º640*480ï¼ï¼ï¼
    //sop æŒ‡ç¤ºç¬¬ä¸€å¹…å›¾åƒçš„ç¬¬ä¸€ä¸ªæ•°æ®
    //eop æŒ‡ç¤ºç¬¬ä¸€å‰¯å›¾åƒçš„æœ€åä¸€ä¸ªæ•°æ®
    //vld æŒ‡ç¤ºæœ‰æ•ˆæ•°æ®

    //ä¸Šç”µåï¼Œéœ€è¦åˆ¤æ–­dout_usedw_1å’Œdout_usedw_2å¤§äº200ä¸ªä¹‹åï¼Œæ‰èƒ½æ‹‰é«˜b_rdy_1 ï¼Œb_rdy_2ï¼Œè¯·æ±‚è¾“å‡ºæ•°æ®
    // b_rdy_1 å’Œ b_rdy_2 éœ€è¦åŒæ—¶æ‹‰é«˜ ï¼Œä»¥ç¡®ä¿åŒæ—¶ç»“æŸï¼ï¼ï¼ï¼


    //å·¥ä½œæµç¨‹
    //1ã€è¾“å…¥çš„16ä½æ•°æ®æµè¿›å…¥â€œæ€»çº¿ä½å®½è½¬æ¢æ¨¡å—â€æŠŠ16ä½æ•°æ®è½¬æ¢æˆ48ä½æ•°æ®ï¼Œ
    //   å¹¶ä¸”è¿›è¡Œâ€œå¤´åˆ¤æ–­â€å³æ”¶åˆ°sopæ‰å¼€å§‹ç¼“å­˜æ•°æ®ç›´åˆ°eop,ä¹‹åä¸å†å†™å…¥æ•°æ®ï¼Œç›´åˆ°æ¥æ”¶â€œåˆ‡æ¢RAMâ€ï¼ˆflag_sw_ff3ï¼‰ ä¿¡å·æ‰èƒ½å†æ¬¡å†™å…¥

    //2ã€åˆ¤æ–­â€œå†™å…¥FIFOâ€é‡Œé¢çš„æ•°é‡ï¼Œå¦‚æœ>256ï¼ˆSDRAMä¸€é¡µçš„æ•°æ®é‡ï¼‰å°±è¯»å‡ºæ•°æ®å¹¶ä¸”å†™å…¥SDRAM

    //3ã€4è·¯FIFOçš„ä¼˜å…ˆçº§æ˜¯ wr_color > wr_sobel > rd_color > rd_sobel
    //   å½“å†™å…¥æˆ–è€…è¯»å‡ºå®Œä¸€é¡µï¼ˆ256ä¸ªæ•°æ®ï¼‰åå†è¿›è¡Œä¼˜å…ˆçº§åˆ¤æ–­
    //

    //4ã€å½“â€œè¯»å‡ºFIFOâ€å†…çš„æ•°æ®é‡  < 256ï¼ˆSDRAMä¸€é¡µçš„æ•°æ®é‡ï¼‰ å°±è¯»å‡ºSDRAMçš„æ•°æ®å†™å…¥FIFO

    //5ã€è¾“å…¥çš„å›¾åƒæ˜¯30HZï¼Œè€Œè¾“å‡ºçš„å›¾åƒæ˜¯60HZ
    //   å½“è¾“å…¥å®Œæˆä¸€å‰¯å›¾åƒä¹‹åï¼Œå°±ä¸å†å†™å…¥å›¾åƒï¼Œç­‰åˆ°è¯»å‡ºå®Œæˆä¸€å¹…å›¾åƒä¹‹åï¼Œè¿›è¡Œåˆ‡æ¢RAMåœ°å€ï¼Œæ‰èƒ½å†æ¬¡å†™å…¥ï¼Œå¹¶ä¸”æŠŠåˆšæ‰å†™å…¥çš„å›¾åƒè¾“å‡º

    //6ã€å†™å…¥å®Œæˆæ ‡å¿—ä½ï¼Œåªèƒ½åœ¨åˆ‡æ¢RAMçš„ä½¿ç”¨æ¸…é›¶ï¼Œä½†æ˜¯è¯»å‡ºå®Œæˆæ ‡å¿—ä½ï¼Œåœ¨ä¸‹ä¸€æ¬¡å¼€å§‹çš„æ—¶å€™æ¸…é›¶ï¼ˆcolor_new_startï¼‰ æˆ–è€… åœ¨åˆ‡æ¢RAMçš„æ—¶å€™æ¸…é›¶ï¼ˆping_pong_endï¼‰
    //   åˆ‡æ¢RAMçš„æ¡ä»¶æ˜¯ 4ä¸ªæ•°æ®æµéƒ½ä¼ è¾“å®Œæˆ ping_pong_end 


    //æµ‹è¯•æ¥å£ï¼Œç”¨ååˆ é™¤
    output wr_color_en;
    output wr_sobel_en;
    output rd_color_en;
    output rd_sobel_en;

    output rd_sobel_end;
    output rd_color_end; 
    output wr_sobel_end;
    output wr_color_end;



    //å®šä¹‰4ç‰‡å†…å­˜å—çš„åœ°å€ ç”¨æ¥å­˜æ”¾ â€œè¯»â€å½©è‰²å’ŒäºŒå€¼   å’Œ   â€œå†™â€å½©è‰²å’ŒäºŒå€¼
    //è¿›è¡Œä¹’ä¹“æ“ä½œ
    //[13:12] = bank åœ°å€
    //[11: 0] = èµ·å§‹åœ°å€
    parameter BANK_1 = 14'b00_000000000000;//å†…å­˜å— 1
    parameter BANK_2 = 14'b01_000000000000;//å†…å­˜å— 2
    parameter BANK_3 = 14'b10_000000000000;//å†…å­˜å— 3
    parameter BANK_4 = 14'b11_000000000000;//å†…å­˜å— 4

    //æ¯ä¸ªç”»é¢æœ‰å¤šå°‘è¡Œ
    //640*480*16bit / 256 / 48 
    parameter PIC_ROW = 400;//æ¯å¹…ç”»é¢ å SDRAM 400è¡Œï¼ˆé¡µï¼‰
    parameter SD_PAGE = 256;//SDRAM ä¸€é¡µæ˜¯256ä¸ª


    output      [ 1:0]          flag_sel    ;
    output                      end_cnt0    ;
    output                      end_cnt1    ;
    output                      add_cnt0    ;
    output                      add_cnt1    ;


    input                       clk         ;
    input                       clk_100M    ;
    input                       rst_n       ;
    input       [3:0]           key_vld     ;

    //ç¡¬ä»¶æ¥å£
    input       [47:0]          dq_in       ;

    output                      sd_clk      ;//SDRAMæ—¶é’Ÿ  å–åè¾“å…¥æ—¶é’Ÿå¾—åˆ°
    output                      cke         ;
    output                      cs          ;
    output                      ras         ;
    output                      cas         ;
    output                      we          ;
    output      [ 5:0]          dqm         ;
    output      [11:0]          sd_addr     ;
    output      [ 1:0]          sd_bank     ;
    output      [47:0]          dq_out      ;//å…¨éƒ¨SDRAMéƒ½ç”¨ä¸Š
    output                      dq_out_en   ;

    wire                        sd_clk      ;//SDRAMæ—¶é’Ÿ  å–åè¾“å…¥æ—¶é’Ÿå¾—åˆ°
    wire                        cke         ;
    wire                        cs          ;
    wire                        ras         ;
    wire                        cas         ;
    wire                        we          ;
    wire        [ 5:0]          dqm         ;
    wire        [11:0]          sd_addr     ;
    wire        [ 1:0]          sd_bank     ;
    wire        [47:0]          dq_out      ;//å…¨éƒ¨SDRAMéƒ½ç”¨ä¸Š
    wire                        dq_out_en   ;


    //æ•°æ®è¾“å…¥æ¥å£
    input       [15:0]      din_1           ;//å½©è‰²å›¾åƒ
    input                   din_vld_1       ;
    input                   din_sop_1       ;
    input                   din_eop_1       ;

    input       [15:0]      din_2           ;//äºŒå€¼å›¾åƒ
    input                   din_vld_2       ;
    input                   din_sop_2       ;
    input                   din_eop_2       ;

    input                   b_rdy_1         ;
    input                   b_rdy_2         ;

    //æ•°æ®è¾“å‡ºæ¥å£
    output      [15:0]      dout_1          ;//å½©è‰²å›¾åƒ
    output                  dout_vld_1      ;
    output                  dout_sop_1      ;
    output                  dout_eop_1      ;
    output      [ 8:0]      dout_usedw_1    ;

    output      [15:0]      dout_2          ;//äºŒå€¼å›¾åƒ
    output                  dout_vld_2      ;
    output                  dout_sop_2      ;
    output                  dout_eop_2      ;
    output      [ 8:0]      dout_usedw_2    ;

    wire        [15:0]      dout_1          ;//å½©è‰²å›¾åƒ
    wire                    dout_vld_1      ;
    wire                    dout_sop_1      ;
    wire                    dout_eop_1      ;
    wire        [ 8:0]      dout_usedw_1    ;

    wire        [15:0]      dout_2          ;//äºŒå€¼å›¾åƒ
    wire                    dout_vld_2      ;
    wire                    dout_sop_2      ;
    wire                    dout_eop_2      ;
    wire        [ 8:0]      dout_usedw_2    ;

    //ä¸­é—´ä¿¡å·
    wire        [47:0]      color_in        ;
    wire                    color_in_sop    ;
    wire                    color_in_eop    ;
    wire                    color_in_vld    ;

    wire        [47:0]      sobel_in        ;
    wire                    sobel_in_sop    ;
    wire                    sobel_in_eop    ;
    wire                    sobel_in_vld    ;   

    wire        [ 8:0]      wr_usedw_color  ;
    wire        [ 8:0]      wr_usedw_sobel  ;
    wire        [ 8:0]      rd_usedw_color  ;
    wire        [ 8:0]      rd_usedw_sobel  ;

    
    reg                     wr_color_rdy_start;
    reg                     wr_sobel_rdy_start;
    wire                    wr_color_rdy    ;
    wire                    wr_sobel_rdy    ;
    wire                    rd_color_rdy    ;
    wire                    rd_sobel_rdy    ;

    reg         [ 1:0]      rw_bank         ;
    reg         [11:0]      rw_addr         ;
	 reg                     stop            ;

    wire        [47:0]      wdata           ; 
    wire                    wr_ack          ;
    reg                     wr_req          ;

    reg                     rd_req          ;
    wire                    rd_ack          ;
    wire        [47:0]      rdata           ;

    reg                     flag_sw_ff0     ;
    reg                     flag_sw_ff1     ;
    reg                     flag_sw_ff2     ;
    reg                     flag_sw_ff3     ;

    reg                     work_flag       ;
    reg         [ 1:0]      flag_sel        ;
    wire                    work_flag_start ;
    wire                    work_flag_stop  ;
    wire                    wr_color_en     ;
    wire                    wr_sobel_en     ;    
    wire                    rd_color_en     ;
    wire                    rd_sobel_en     ;

    wire                    ping_pong_end   ;
    reg                     rw_addr_sel     ;
    reg                     wr_color_end    ;
    reg                     wr_sobel_end    ;
    reg                     rd_color_end    ;
    reg                     rd_sobel_end    ;
    wire                    sobel_new_start ;
    wire                    color_new_start ;

    reg                     wr_flag         ;

    wire                    add_cnt0        ;
    wire                    end_cnt0        ;
    reg         [ 8:0]      cnt0            ;

    wire                    add_cnt1        ;
    wire                    end_cnt1        ;
    reg         [ 9:0]      cnt1            ;

    wire                    add_cnt2        ;
    wire                    end_cnt2        ;
    reg         [ 9:0]      cnt2            ;

    wire                    add_cnt3        ;
    wire                    end_cnt3        ;
    reg         [ 9:0]      cnt3            ;

    wire                    add_cnt4        ;
    wire                    end_cnt4        ;
    reg         [ 9:0]      cnt4            ;

    wire                    add_cnt5        ;
    wire                    end_cnt5        ;
    reg         [ 2:0]      cnt5            ;

    reg                     color_out_vld   ;
    reg                     color_out_sop   ;
    reg                     color_out_eop   ;

    reg                     sobel_out_vld   ;
    reg                     sobel_out_sop   ;
    reg                     sobel_out_eop   ;

    reg         [47:0]      color_out       ;
    reg         [47:0]      sobel_out       ;

    wire                    rd_vld          ;



    //å½©è‰²å›¾åƒ 
    bus_conv_16_to_48 color_in_fifo(
        .clk                (clk            ),
        .clk_out            (clk_100M       ),
        .rst_n              (rst_n          ),

        .din                (din_1          ),
        .din_sop            (din_sop_1      ),
        .din_eop            (din_eop_1      ),
        .din_vld            (din_vld_1      ),

        .dout               (color_in       ),
        .dout_sop           (color_in_sop   ),
        .dout_eop           (color_in_eop   ),
        .dout_vld           (color_in_vld   ),
        .dout_mty           (),

        .b_rdy              (wr_color_rdy   ),
        .rd_usedw           (wr_usedw_color ),
        .flag_sw            (flag_sw_ff3    )//25M  æ—¶é’ŸåŸŸ
    );


    bus_conv_16_to_48 sobel_in_fifo(
        .clk                (clk            ),
        .clk_out            (clk_100M       ),
        .rst_n              (rst_n          ),

        .din                (din_2          ),
        .din_sop            (din_sop_2      ),
        .din_eop            (din_eop_2      ),
        .din_vld            (din_vld_2      ),

        .dout               (sobel_in       ),
        .dout_sop           (sobel_in_sop   ),
        .dout_eop           (sobel_in_eop   ),
        .dout_vld           (sobel_in_vld   ),
        .dout_mty           (),

        .b_rdy              (wr_sobel_rdy   ),
        .rd_usedw           (wr_usedw_sobel ),
        .flag_sw            (flag_sw_ff3    )//25M  æ—¶é’ŸåŸŸ
    );

    bus_conv_48to_16 color_out_fifo(
        .clk                (clk_100M       ),
        .clk_out            (clk            ),
        .rst_n              (rst_n          ),

        .din                (color_out      ),
        .din_sop            (color_out_sop  ),
        .din_eop            (color_out_eop  ),
        .din_vld            (color_out_vld  ),
        .din_mty            (3'h0           ),
        .wr_usedw           (rd_usedw_color ),

        .dout               (dout_1         ),
        .dout_sop           (dout_sop_1     ),
        .dout_eop           (dout_eop_1     ),
        .dout_vld           (dout_vld_1     ),
        .dout_mty           (),
        .rd_usedw           (dout_usedw_1   ),
        .b_rdy              (b_rdy_1        )
    );

    bus_conv_48to_16 sobel_out_fifo(
        .clk                (clk_100M       ),
        .clk_out            (clk            ),
        .rst_n              (rst_n          ),

        .din                (sobel_out      ),
        .din_sop            (sobel_out_sop  ),
        .din_eop            (sobel_out_eop  ),
        .din_vld            (sobel_out_vld  ),
        .din_mty            (3'h0           ),

        .dout               (dout_2         ),
        .dout_sop           (dout_sop_2     ),
        .dout_eop           (dout_eop_2     ),
        .dout_vld           (dout_vld_2     ),
        .dout_mty           (),
        .rd_usedw           (dout_usedw_2   ),
        .b_rdy              (b_rdy_2        ),


        .wr_usedw           (rd_usedw_sobel )
    );


    sdram sdram_1(
        .clk                (clk_100M       ),
        .rst_n              (rst_n          ),

        .rw_addr            (rw_addr        ),//è¯»å†™åœ°å€
        .rw_bank            (rw_bank        ),//è¯»å†™çš„bank

        .wdata              (wdata          ),//å†™æ•°æ®
        .wr_ack             (wr_ack         ),//å†™è¯·æ±‚çš„åº”ç­”
        .wr_req             (wr_req         ),//å†™è¯·æ±‚

        .rd_vld             (rd_vld         ),//è¯»æœ‰æ•ˆ
        .rdata              (rdata          ),//è¯»æ•°æ®
        .rd_ack             (rd_ack         ),//è¯»è¯·æ±‚å¾—åˆ°åº”ç­”
        .rd_req             (rd_req         ),//è¯»è¯·æ±‚
    
        .sd_clk             (sd_clk         ),
        .cke                (cke            ),
        .cs                 (cs             ),
        .ras                (ras            ),
        .cas                (cas            ),
        .we                 (we             ),
        .dqm                (dqm            ),
        .sd_addr            (sd_addr        ),
        .sd_bank            (sd_bank        ),
       // .key_vld            (key_vld        ),
        
        .dq_in              (dq_in          ),
        .dq_out             (dq_out         ),
        .dq_out_en          (dq_out_en      )
    );



    //flag_sw   è·¨æ—¶é’ŸåŸŸå¤„ç† 100Måˆ°25MHZ
    //æ–¹æ³•ï¼šæŠŠ ping_pong_end å»¶é•¿åˆ°8ä¸ªæ—¶é’Ÿå‘¨æœŸ ç„¶åé‚£25Mçš„å»é‡‡æ ·ï¼Œå¹¶ä¸”æ‰“3æ‹é˜²æ­¢äºšç¨³æ€
    always @(posedge clk_100M or negedge rst_n)begin
        if(!rst_n)begin
            cnt5 <= 0;
        end
        else if(add_cnt5)begin
            if(end_cnt5)
                cnt5 <= 0;
            else
                cnt5 <= cnt5 + 1;
        end
    end
    assign add_cnt5 = flag_sw_ff0;
    assign end_cnt5 = add_cnt5 && cnt5 == 8-1;
    
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            flag_sw_ff0 <= 0;
        end
        else if(ping_pong_end)begin
            flag_sw_ff0 <= 1;
        end
        else if(end_cnt5)begin
            flag_sw_ff0 <= 0;
        end
    end
    
    //flag_sw_ff3 ä½¿ç”¨
    always  @(posedge clk or negedge rst_n)begin
        if(rst_n==1'b0)begin
            flag_sw_ff1 <= 0;
            flag_sw_ff2 <= 0;
            flag_sw_ff3 <= 0;
        end
        else begin
            flag_sw_ff1 <= flag_sw_ff0;
            flag_sw_ff2 <= flag_sw_ff1;
            flag_sw_ff3 <= flag_sw_ff2;
        end
    end
    
    

 


    //æ ¹æ®FIFOå†…å‰©ä½™æ•°æ®çš„æ•°é‡æ¥å†³å®šå“ªä¸ªFIFOå†™å…¥æˆ–è¯»å‡º
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            work_flag <= 0;
        end
        else if(work_flag_start)begin
            work_flag <= 1;
        end
        else if(work_flag_stop)begin
            work_flag <= 0;
        end
    end

    assign work_flag_start = work_flag == 0 && (wr_color_en || wr_sobel_en || rd_color_en || rd_sobel_en);
    assign work_flag_stop =  work_flag == 1 && end_cnt0;

    //flag_sel é€‰æ‹©è¯»æˆ–è€…å†™ 4ä¸ªå†…å­˜å—ä¸­çš„ä¸€ä¸ª
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            flag_sel <= 0;
        end
        else if(work_flag_start)begin
            //é€‰æ‹©å†…å­˜å—çš„æ ‡å¿—ä½
            if(wr_color_en)
                flag_sel <= 0;
            else if(wr_sobel_en)
                flag_sel <= 1;
            else if(rd_color_en)
                flag_sel <= 2;
            else if(rd_sobel_en)
                flag_sel <= 3;
        end
    end
    //å†™FIFO å¤§äº256ä¸ªæ•°æ®å°±å¼€å§‹å†™å…¥SDRAM
    assign wr_color_en = wr_usedw_color >= SD_PAGE && wr_color_end == 0;
    assign wr_sobel_en = (wr_usedw_sobel >= SD_PAGE && wr_sobel_end == 0) && wr_color_en == 0;


    //assign wr_sobel_en = wr_usedw_color < SD_PAGE && wr_usedw_sobel >= SD_PAGE && wr_sobel_end == 0;

    //è¯»FIFO å°äº256ä¸ªæ•°æ®å°±å¼€å§‹è¯»å–SDRAM
    //åˆ¤æ–­rd_usedw_color rd_usedw_sobel éœ€è¦å‡2 å› ä¸ºusedw æœ‰å»¶æ—¶ï¼Œ2æ˜¯è°ƒå‡ºæ¥çš„
    //assign rd_color_en = rd_usedw_color < SD_PAGE-2 && wr_usedw_color < SD_PAGE && wr_usedw_sobel < SD_PAGE;
    assign rd_color_en = (rd_usedw_color < SD_PAGE-2 && wr_color_en == 0) && wr_sobel_en == 0;



    //æ³¨æ„ï¼šè¿™é‡Œ åˆ¤æ–­ è¾“å‡º color FIFOæ•°é‡è¦ä½¿ç”¨rd_usedw_color >= SD_PAGE ä½¿ç”¨ >= !!!!
    //assign rd_sobel_en = rd_usedw_sobel < SD_PAGE-2 && rd_usedw_color >= SD_PAGE && wr_usedw_color < SD_PAGE && wr_usedw_sobel < SD_PAGE;
    assign rd_sobel_en = (rd_usedw_sobel < SD_PAGE-2) && rd_color_en == 0;



    //æ ¹æ®flag_sel è®¾ç½® è¯»å†™çš„bank åœ°å€
    //æ ¹æ®flag_sel è®¾ç½® è¯»å†™çš„addr åœ°å€
    //rw_addr_sel åˆ‡æ¢ RAMåœ°å€
    always  @(*)begin
        if (rw_addr_sel) begin  //A  ä¹’ä¹“æ“ä½œ
            if(flag_sel == 0)begin
                rw_bank = BANK_1[13:12];
                rw_addr = BANK_1[11:0] + cnt1;
            end
            else if(flag_sel == 1)begin
                rw_bank = BANK_2[13:12];
                rw_addr = BANK_2[11:0] + cnt2;
            end
            else if(flag_sel == 2)begin
                rw_bank = BANK_3[13:12];
                rw_addr = BANK_3[11:0] + cnt3;
            end
            else begin
                rw_bank = BANK_4[13:12];
                rw_addr = BANK_4[11:0] + cnt4; 
            end       
        end 
        else begin              //B
            if(flag_sel == 0)begin
                rw_bank = BANK_3[13:12];
                rw_addr = BANK_3[11:0] + cnt1;
            end
            else if(flag_sel == 1)begin
                rw_bank = BANK_4[13:12];
                rw_addr = BANK_4[11:0] + cnt2;
            end
            else if(flag_sel == 2)begin
                rw_bank = BANK_1[13:12];
                rw_addr = BANK_1[11:0] + cnt3;
            end
            else begin
                rw_bank = BANK_2[13:12];   
                rw_addr = BANK_2[11:0] + cnt4;   
            end
        end
    end
    always  @(posedge clk or negedge rst_n)begin
        if(rst_n==1'b0)begin
            stop <= 1;
        end
        else if(key_vld[2])begin
            stop <= 0;
        end
        else if(key_vld[3])begin
            stop <= 1;
        end
    end
    
    //åˆ‡æ¢RAM
    //ä¹’ä¹“æ“ä½œ
    //ä¹’ä¹“æ“ä½œçš„ç»“æŸæ¡ä»¶ï¼šå†™å…¥SDRAMå®Œæˆï¼Œè¯»å‡ºSDRAMå®Œæˆ
    //assign ping_pong_end = rd_sobel_end && rd_color_end && wr_sobel_end && wr_color_end;
	
    assign ping_pong_end = rd_color_end && wr_sobel_end && wr_color_end && stop;//ä¿®æ­£äºŒå€¼å›¾åƒåç§» ä¿®æ­£å›¾åƒåç§»ï¼Œå¯èƒ½æœ‰é—®é¢˜ ï¼ï¼ï¼ï¼
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            rw_addr_sel <= 0;
        end
        else if(ping_pong_end)begin
            rw_addr_sel <= ~rw_addr_sel;
        end
    end

/********************************************************************/
    //ä¼ è¾“å®Œæˆæ ‡å¿—ä½
    //å†™å…¥ï¼š
    //å†™å…¥å®Œæˆä¸€å¸§å›¾åƒä¹‹ ç­‰åˆ° åˆ‡æ¢RAMä¹‹åæ‰ä¼šç»§ç»­å†™å…¥æ•°æ®
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            wr_color_end <= 0;
        end
        else if(end_cnt1)begin
            wr_color_end <= 1;
        end
        else if(ping_pong_end)begin
            wr_color_end <= 0;
        end
    end
    
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            wr_sobel_end <= 0;
        end
        else if(end_cnt2)begin
            wr_sobel_end <= 1;
        end
        else if(ping_pong_end)begin
            wr_sobel_end <= 0;
        end
    end


    //è¯»å‡ºå®Œæˆä¹‹åæ ‡å¿—ä½ ç½®ä¸€ å¦‚æœ â€œå…¨éƒ¨â€ å†™å…¥å®Œæˆ å’Œ è¯»å‡ºå®Œæˆ åˆ™åˆ‡æ¢RAM ï¼Œå¦åˆ™åœ¨ ä¸‹ä¸€æ¬¡å¼€å§‹å‘é€çš„æ—¶å€™æ¸…é›¶ 
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            rd_color_end <= 0;
        end
        else if(end_cnt3)begin
            rd_color_end <= 1;
        end
        else if(ping_pong_end || color_new_start)begin
            rd_color_end <= 0;
        end
    end
    assign color_new_start = rd_color_end && work_flag && flag_sel == 2;

    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            rd_sobel_end <= 0;
        end
        else if(end_cnt4)begin
            rd_sobel_end <= 1;
        end
        else if(ping_pong_end || sobel_new_start)begin
            rd_sobel_end <= 0;
        end
    end
    assign sobel_new_start = rd_sobel_end && work_flag && flag_sel == 3;

/********************************************************************/   
    //                                                   SDRAM å†™å…¥éƒ¨åˆ†
    //wr_color_rdy  wr_color_rdy  ä¸Šå‡æ²¿å’Œ wr_ack ä¸Šå‡æ²¿å¯¹é½

    //æ³¨æ„ï¼šä½¿ç”¨wr_color_rdy_start ä½¿èƒ½è®¡æ•°å™¨ å¯¹é½æ—¶åºï¼ï¼ï¼ï¼ï¼
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            wr_color_rdy_start <= 0;
        end
        else if(wr_color_rdy)begin
            wr_color_rdy_start <= 1;
        end
        else begin
            wr_color_rdy_start <= 0;
        end
    end
    
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            wr_sobel_rdy_start <= 0;
        end
        else if(wr_sobel_rdy)begin
            wr_sobel_rdy_start <= 1;
        end
        else begin
            wr_sobel_rdy_start <= 0;
        end
    end
    assign wr_color_rdy = (wr_flag || wr_ack) && flag_sel == 0 && work_flag && end_cnt0 == 0;//è¯·æ±‚è¯»å‡ºFIFOå†…çš„ å½©è‰²å›¾åƒ è¯·æ±‚ 
    assign wr_sobel_rdy = (wr_flag || wr_ack) && flag_sel == 1 && work_flag && end_cnt0 == 0;//è¯·æ±‚è¯»å‡ºFIFOå†…çš„ äºŒå€¼å›¾åƒ è¯·æ±‚

    assign wdata = (flag_sel == 0) ? color_in : sobel_in;//å†™å…¥SDRAMçš„æ•°é€šæºé€‰æ‹©

    //äº§ç”Ÿå†™è¯·æ±‚
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            wr_req <= 0;
        end
        else if(work_flag_start && (wr_color_en || wr_sobel_en))begin
            wr_req <= 1;
        end
        else if(wr_ack) 
            wr_req <= 0;
    end

    //æ”¶åˆ°SDRAMçš„ wr_ack ä¹‹åå¼€å§‹å†™å…¥æ•°æ®
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            wr_flag <= 0;
        end
        else if(wr_ack && wr_flag == 0)begin
            wr_flag <= 1;
        end
        else if(end_cnt0 && wr_flag == 1)begin
            wr_flag <= 0;
        end
    end

    //è¯»å†™ ä¸ªæ•° è®¡æ•°å™¨
    always @(posedge clk_100M or negedge rst_n)begin
        if(!rst_n)begin
            cnt0 <= 0;
        end
        else if(add_cnt0)begin
            if(end_cnt0)
                cnt0 <= 0;
            else
                cnt0 <= cnt0 + 1;
        end
    end
    assign add_cnt0 = wr_color_rdy_start || wr_sobel_rdy_start || rd_color_rdy || rd_sobel_rdy;
    assign end_cnt0 = add_cnt0 && cnt0 == SD_PAGE -1;
    
    // always  @(posedge clk or negedge rst_n)begin
    //     if(rst_n==1'b0)begin
    //         flag_add <= 0;
    //     end
    //     else if(wr_color_busy || wr_sobel_busy || rd_color_busy || rd_sobel_busy)begin
    //         flag_add <= 1;
    //     end
    //     else if(end_cnt0)begin
    //         flag_add <= 0;
    //     end
    // end
    

    //å†™å…¥ å½©è‰²å›¾åƒ åœ°å€â€œè¡Œâ€åœ°å€è®¡æ•°å™¨
    always @(posedge clk_100M or negedge rst_n)begin
        if(!rst_n)begin
            cnt1 <= 0;
        end
        else if(add_cnt1)begin
            if(end_cnt1)
                cnt1 <= 0;
            else
                cnt1 <= cnt1 + 1;
        end
    end
    assign add_cnt1 = end_cnt0 && flag_sel == 0 && work_flag;
    assign end_cnt1 = add_cnt1 && cnt1 == PIC_ROW - 1;
    

    //å†™å…¥ äºŒå€¼å›¾åƒ åœ°å€â€œè¡Œâ€åœ°å€è®¡æ•°å™¨
    always @(posedge clk_100M or negedge rst_n)begin
        if(!rst_n)begin
            cnt2 <= 0;
        end
        else if(add_cnt2)begin
            if(end_cnt2)
                cnt2 <= 0;
            else
                cnt2 <= cnt2 + 1;
        end
    end
    assign add_cnt2 = end_cnt0 && flag_sel == 1 && work_flag;
    assign end_cnt2 = add_cnt2 && cnt2 == PIC_ROW - 1;
    
    //è¯»å‡º å½©è‰²å›¾åƒ åœ°å€â€œè¡Œâ€åœ°å€è®¡æ•°å™¨
    always @(posedge clk_100M or negedge rst_n)begin
        if(!rst_n)begin
            cnt3 <= 0;
        end
        else if(add_cnt3)begin
            if(end_cnt3)
                cnt3 <= 0;
            else
                cnt3 <= cnt3 + 1;
        end
    end
    assign add_cnt3 = end_cnt0 && flag_sel == 2 && work_flag;
    assign end_cnt3 = add_cnt3 && cnt3 == PIC_ROW - 1;
    
    //è¯»å‡º äºŒå€¼å›¾åƒ åœ°å€â€œè¡Œâ€åœ°å€è®¡æ•°å™¨
    always @(posedge clk_100M or negedge rst_n)begin
        if(!rst_n)begin
            cnt4 <= 0;
        end
        else if(add_cnt4)begin
            if(end_cnt4)
                cnt4 <= 0;
            else
                cnt4 <= cnt4 + 1;
        end
    end
    assign add_cnt4 = end_cnt0 && flag_sel == 3 && work_flag;
    assign end_cnt4 = add_cnt4 && cnt4 == PIC_ROW - 1;
    
    
/********************************************************************/   
    //                                                   SDRAM è¯»å–éƒ¨åˆ†    
    //è¾“å‡ºæ•°æ®é€‰æ‹©
    //æ³¨æ„ï¼šè¿™é‡Œè¦ä½¿ç”¨æ—¶åºé€»è¾‘ï¼Œå’ŒVLD SOP EOP å¯¹é½
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            color_out <= 0;
        end
        else begin
            color_out <= rdata;
        end
    end
    
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            sobel_out <= 0;
        end
        else begin
            sobel_out <= rdata;
        end
    end


    //äº§ç”Ÿè¯»è¯·æ±‚
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            rd_req <= 0;
        end
        else if(work_flag_start && (rd_color_en || rd_sobel_en))begin
            rd_req <= 1;
        end
        else if(rd_ack)begin
            rd_req <= 0;
        end
    end
    
    assign rd_color_rdy = rd_vld && flag_sel == 2 && work_flag;
    assign rd_sobel_rdy = rd_vld && flag_sel == 3 && work_flag;

    //                                              å½©è‰²å›¾åƒè¾“å‡ºä¿¡å·
    //color_out_vld 
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            color_out_vld <= 0;
        end
        else if(add_cnt0 && flag_sel == 2)begin
            color_out_vld <= 1;
        end
        else begin
            color_out_vld <= 0;
        end
    end

    //color_out_sop
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            color_out_sop <= 0;
        end
        else if(add_cnt0 && cnt0 == 1-1 && cnt3 == 1-1 && flag_sel == 2)begin
            color_out_sop <= 1;
        end
        else begin
            color_out_sop <= 0;
        end
    end
    
    //color_out_eop
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            color_out_eop <= 0;
        end
        else if(end_cnt3)begin
            color_out_eop <= 1;
        end
        else begin
            color_out_eop <= 0;
        end
    end
    
    

    //                                              äºŒå€¼å›¾åƒè¾“å‡ºä¿¡å·
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            sobel_out_vld <= 0;
        end
        else if(add_cnt0 && flag_sel == 3)begin
            sobel_out_vld <= 1;
        end
        else begin
            sobel_out_vld <= 0;
        end
    end

    //sobel_out_sop
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            sobel_out_sop <= 0;
        end
        else if(add_cnt0 && cnt0 == 1-1 && cnt4 == 1-1 && flag_sel == 3)begin
            sobel_out_sop <= 1;
        end
        else begin
            sobel_out_sop <= 0;
        end
    end
    
    //sobel_out_eop
    always  @(posedge clk_100M or negedge rst_n)begin
        if(rst_n==1'b0)begin
            sobel_out_eop <= 0;
        end
        else if(end_cnt4)begin
            sobel_out_eop <= 1;
        end
        else begin
            sobel_out_eop <= 0;
        end
    end
    
    

endmodule
